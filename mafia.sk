
command /마피아 [<text>] [<text>]:
    aliases: /akvldk
    trigger:
        if arg 1 is "채널":
            if arg 2 is "입장":
                send "마피아 채널에 입장했습니다."
                set {mafia.%player%.isChat} to true
                stop
            if arg 2 is "퇴장":
                send "마피아 채널에 퇴장했습니다."
                set {mafia.%player%.isChat} to false
                stop
        if arg 1 is "참가":
            if arg 2 is "취소":
                if {mafia.%player%.isAttend} is true:
                    send "마피아 게임에 참가 취소했습니다."
                    set {mafia.%player%.isAttend} to false
                    remove 1 from {mafia.count}
                    remove "%player%" from {mafia.players::*}
                stop
            if {mafia.%player%.isAttend} is false:
                send "마피아 게임에 참가했습니다."
                set {mafia.%player%.isAttend} to true
                add "%player%" to {mafia.players::*}
                add 1 to {mafia.count}

                # 7명이 되었을 때
                if {mafia.count} is 5:
                    broadcast "마피아 참가 인원이 5명이 되었습니다!"
                    broadcast "/마피아 참가"
                # 8명이 되었을 때
                if {mafia.count} is 6:
                    broadcast "10초 뒤 마피아가 시작됩니다."
                    broadcast "10초안에 채널에 입장하지 않은 경우 정상적인 진행이 불가능합니다."
                    broadcast "/마피아 채널 입장 ( 구경 가능 )"
                    wait 10 seconds
                    execute console command "/mfstart"
            stop
        if arg 1 is "정보":
            send "-----------------------------------------------------"
            send "마피아 참가자 명단 (%{mafia.count}%/6) - 6명이 되면 시작합니다!"
            loop {mafia.players::*}:
                send "- %loop-value%"
            send "-----------------------------------------------------"
            stop
        send "-----------------------------------------------------"
        send "/마피아 채널 입장/퇴장"
        send "/마피아 참가"
        send "/마피아 참가 취소"
        send "/마피아 정보"
        send "-----------------------------------------------------"

command /mfinit:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop
        
        if {mafia.bar} is set:
            loop {mafia.players::*}:
                set {_p} to "%loop-value%" parsed as player
                remove {_p} from bossbar {mafia.bar}
        
        if {mafia.isClick} is set:
            clear {mafia.isClick}

        clear {mafia.players::*}
        loop 6 times:
            set {mafia.jobs::%loop-value%} to 0
        set {mafia.isStart} to false
        set {mafia.isNight} to false
        set {mafia.isVote} to false
        set {mafia.count} to 0
        set {mafia.mcount} to 2
        set {mafia.ccount} to 4
        set {mafia.numerator} to 0
        set {mafia.denominator} to 0
        
        loop all players:
            set {mafia.%loop-player%.isChat} to false
            set {mafia.%loop-player%.isAttend} to false
            set {mafia.%loop-player%.jobCode} to 4
            set {mafia.%loop-player%.click_player} to -1

        set {mafia.click_format::1} to 10
        set {mafia.click_format::2} to 12
        set {mafia.click_format::3} to 14
        set {mafia.click_format::4} to 16
        set {mafia.click_format::5} to 28
        set {mafia.click_format::6} to 30
        set {mafia.click_format::7} to 32
        set {mafia.click_format::8} to 34

command /mfday:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop

        set {mafia.isNight} to false

        set {_i} to 1
        loop 6 times:
            set {_n} to "%{mafia.players::%{_i}%}%"

            if {mafia.%{_n}%.jobCode} is not 4:
                set {mafia.%{_n}%.isClick} to false
                if {mafia.%{_n}%.click_player} is not -1:
                    if {mafia.%{_n}%.jobCode} is 1:
                        if {_a1} is not set:
                            set {_a1} to {mafia.%{_n}%.click_player}
                        else:
                            set {_a2} to {mafia.%{_n}%.click_player}

                    else if {mafia.%{_n}%.jobCode} is 3:
                        set {_b} to {mafia.%{_n}%.click_player}
            
            add 1 to {_i}
        
        set {_a} to -1
        if {_a2} is set:
            set {_t} to random integer between 1 to 2
            if {_t} is 1:
                set {_a} to {_a1}
            else:
                set {_a} to {_a2}
        else:
            set {_a} to {_a1}
        
        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                send "-----------------------------------------------------" to loop-player
                send "낮이 되었습니다" to loop-player
                if {_a} is {_b}:
                    send "의사의 도움으로 %{mafia.players::%{_a}%}%님이 치명상에서 벗어났습니다." to loop-player
                else:
                    if {_a} is not -1:
                        send "%{mafia.players::%{_a}%}%님이 사망하셨습니다." to loop-player
                    else:
                        send "아무일도 일어나지 않았습니다." to loop-player
                send "-----------------------------------------------------" to loop-player
        
        if {_a} is not -1:
            if {mafia.%{mafia.players::%{_a}%}%.jobCode} is 1:
                remove 1 from {mafia.mcount}
            else:
                remove 1 from {mafia.ccount}
            set {mafia.%{mafia.players::%{_a}%}%.jobCode} to 4
        
        clear {_a1}
        clear {_a2}
        clear {_a}

        if {mafia.ccount} <= 0:
            execute console command "/mfclose 0"
            stop
        if {mafia.mcount} <= 0:
            execute console command "/mfclose 1"
            stop
        
        execute console command "/mfvote"

command /mfvote:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop

        set {mafia.isVote} to true
        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                set {mafia.%loop-player%.isClick} to false
                send "-----------------------------------------------------" to loop-player
                send "투표 시간입니다." to loop-player
                send "5분동안 상의 후에 각자 마피아로 의심가는 인물 한 명을 지목해주시길 바랍니다." to loop-player
                send "/투표" to loop-player
                send "-----------------------------------------------------" to loop-player
        
        set skellett title of bossbar {mafia.bar} to "남은 투표 시간 : 180초"
        set skellett progress of bossbar {mafia.bar} to 1 
        set {mafia.denominator} to 180
        set {mafia.numerator} to {mafia.denominator}

        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                skellett add loop-player to bossbar {mafia.bar}

        wait 3 minutes
        if {mafia.isStart} is true:
            if {mafia.isClick} is set:
                clear {mafia.isClick}
            execute console command "/mfvoteclose"

command /mfvoteclose:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop

        set {mafia.isVote} to false

        set {_i} to 1
        loop 6 times:
            set {_n} to "%{mafia.players::%{_i}%}%"
            set {_tmp::%{_i}%} to 0

            add 1 to {_tmp::%{mafia.%{_n}%.click_player}%}
            add 1 to {_i}        

        set {_s} to 0
        set {_t} to 0
        set {_i} to 1
        loop {_tmp::*}:
            if {_s} <= {_tmp::%{_i}%}:
                set {_t} to {_i}
                set {_s} to {_tmp::%{_i}%}
            add 1 to {_i}

        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                send "-----------------------------------------------------" to loop-player
                send "투표가 종료되었습니다." to loop-player
                send "가장 득표율이 높은 [%{mafia.players::%{_t}%}%]님이 %{_s}%표로 사망하셨습니다." to loop-player
                if {mafia.%{mafia.players::%{_t}%}%.jobCode} is 1:
                    send "[%{mafia.players::%{_t}%}%]님은 [&c마피아&f]였습니다!" to loop-player
                else:
                    send "[%{mafia.players::%{_t}%}%]님은 [&c마피아&f]가 아니었습니다!" to loop-player
                send "-----------------------------------------------------" to loop-player

        if {mafia.%{mafia.players::%{_t}%}%.jobCode} is 1:
            remove 1 from {mafia.mcount}
        else:
            remove 1 from {mafia.ccount}
        set {mafia.%{mafia.players::%{_t}%}%.jobCode} to 4
        
        if {mafia.ccount} <= 0:
            execute console command "/mfclose 0"
            stop
        if {mafia.mcount} <= 0:
            execute console command "/mfclose 1"
            stop

        execute console command "/mfnight"

command /mfnight:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop
        
        set {mafia.isNight} to true
        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                set {mafia.%loop-player%.isClick} to false
                send "-----------------------------------------------------" to loop-player
                send "밤이 되었습니다." to loop-player
                send "1분동안 각자 직업에 맞는 행동을 해주시길 바랍니다." to loop-player
                send "/직업" to loop-player
                send "-----------------------------------------------------" to loop-player

        set skellett title of bossbar {mafia.bar} to "남은 밤 시간 : 60초"
        set skellett progress of bossbar {mafia.bar} to 1 
        set {mafia.denominator} to 60
        set {mafia.numerator} to {mafia.denominator}

        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                skellett add loop-player to bossbar {mafia.bar}
        
        wait 1 minutes
        if {mafia.isStart} is true:
            execute console command "/mfday"

command /mfstart:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop

        loop 6 times:
            set {_n} to "%{mafia.players::%loop-value%}%"
            set {mafia.jobs::%loop-value%} to 0
            set {mafia.%{_n}%.jobCode} to 0
            set {mafia.%{_n}%.click_player} to -1

        # 마피아
        set {_c} to 0
        while {_c} < 2:
            set {_x} to random integer from 1 to 6
            if {mafia.jobs::%{_x}%} is 0:
                set {mafia.jobs::%{_x}%} to 1
                set {mafia.%{mafia.players::%{_x}%}%.jobCode} to 1
                add 1 to {_c}
                
        # 경찰
        set {_c} to 0
        while {_c} < 1:
            set {_x} to random integer from 1 to 6
            if {mafia.jobs::%{_x}%} is 0:
                set {mafia.jobs::%{_x}%} to 2
                set {mafia.%{mafia.players::%{_x}%}%.jobCode} to 2
                add 1 to {_c}
                
        # 의사
        set {_c} to 0
        while {_c} < 1:
            set {_x} to random integer from 1 to 6
            if {mafia.jobs::%{_x}%} is 0:
                set {mafia.jobs::%{_x}%} to 3
                set {mafia.%{mafia.players::%{_x}%}%.jobCode} to 3
                add 1 to {_c}
        
        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                send "-----------------------------------------------------" to loop-player
                send "마피아가 시작되었습니다!!" to loop-player
                loop {mafia.players::*}:
                    set {_p} to "%loop-value-2%" parsed as player
                    if {mafia.jobs::%loop-index%} is 0:
                        send "당신의 직업은 [&e시민&f]입니다." to {_p}
                    if {mafia.jobs::%loop-index%} is 1:                            
                        send "당신의 직업은 [&c마피아&f]입니다." to {_p}
                        send "서로 마피아끼리 채팅으로 확인하신 후 투표 진행해주세요." to {_p}
                        send "서로 다른 사람을 지목한 경우 둘 중 랜덤한 인물에게 적용됩니다." to {_p}
                    if {mafia.jobs::%loop-index%} is 2:
                        send "당신의 직업은 [&9경찰&f]입니다." to {_p}
                        send "하루에 한 번씩 특정인의 직업을 알 수 있습니다." to {_p}
                        send "선택 후엔 되돌릴 수 없으니 신중하게 선택해주시길 바랍니다." to {_p}
                    if {mafia.jobs::%loop-index%} is 3:
                        send "당신의 직업은 [의사]입니다." to {_p}
                        send "하루에 한 번씩 특정인을 살릴 수 있습니다." to {_p}
                send "-----------------------------------------------------" to loop-player

        set {mafia.isStart} to true
        execute console command "/mfnight"

command /mfclose <number>:
    trigger:
        if command sender is player:
            send "권한이 없습니다."
            stop
    
        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                send "-----------------------------------------------------" to loop-player
                send "마피아가 종료 되었습니다." to loop-player
                if arg 1 is 0:
                    send "결과 : &c마피아&f팀의 승리!!" to loop-player
                else:
                    send "결과 : &e시민&f팀의 승리!!" to loop-player
                send "마피아는" to loop-player
                loop {mafia.players::*}:
                    if {mafia.jobs::%loop-index%} is 1:
                        send "- %loop-value-2%" to loop-player-1
                send "이었습니다!!" to loop-player
                send "-----------------------------------------------------" to loop-player

        loop {mafia.players::*}:
            set {_p} to "%loop-value%" parsed as player

            send "-----------------------------------------------------" to {_p}
            if {mafia.jobs::%loop-index%} is 1:
                if arg 1 is 0:
                    # SkRayFall 에드온 필요
                    # send {_p} title "&9승리!" with subtitle "&f마피아의 승리" for 5 seconds
                    send "&9승리!" to {_p}
                else:
                    # SkRayFall 에드온 필요
                    # send {_p} title "&c패배.." with subtitle "&f시민의 승리" for 5 seconds
                    send "&c패배.." to {_p}
            else if {mafia.jobs::%loop-index%} is 4:
                if arg 1 is 0:
                    # SkRayFall 에드온 필요
                    # send {_p} title "&0결과" with subtitle "&f마피아의 승리" for 5 seconds
                    send "결과: 마피아팀의 승리" to {_p}
                else:
                    # SkRayFall 에드온 필요
                    # send {_p} title "&0결과" with subtitle "&f시민의 승리" for 5 seconds
                    send "결과: 마피아팀의 승리" to {_p}
            else:   
                if arg 1 is 0:
                    # SkRayFall 에드온 필요
                    # send {_p} title "&9패배.." with subtitle "&f마피아의 승리" for 5 seconds
                    send "&c패배.." to {_p}
                else:
                    # SkRayFall 에드온 필요
                    # send {_p} title "&c승리!" with subtitle "&f시민의 승리" for 5 seconds
                    send "&9승리!" to {_p}
            send "-----------------------------------------------------" to {_p}
        
        execute console command "/mfinit"

command /투표:
    aliases: /xnvy
    trigger:
        if {mafia.%player%.jobCode} is 4:
            send "권한이 없습니다."
            stop
        if {mafia.isStart} is false:
            send "시작하지 않았습니다."
            stop
        if {mafia.isNight} is true:
            send "낮이 아닙니다."
            stop
        if {mafia.isVote} is false:
            send "투표 시간이 아닙니다."
            stop

        open chest with 5 rows named "투표" to player

        loop 6 times:
            set {_n} to "%{mafia.players::%loop-value%}%"
            if {mafia.%{_n}%.jobCode} is not 4:
                set slot {mafia.click_format::%loop-value%} of current inventory of player to skull of "%{_n}%" parsed as player named "&f%{_n}%"

command /직업:
    aliases: /wlrdjq
    trigger:
        if {mafia.%player%.jobCode} is 4:
            send "유령은 권한이 없습니다."
            stop
        if {mafia.%player%.jobCode} is 0:
            send "당신은 &e시민&f입니다."
            stop
        if {mafia.isStart} is false:
            send "시작하지 않았습니다."
            stop
        
        if {mafia.%player%.jobCode} is 1:
            send "당신은 &c마피아&f입니다."
            if {mafia.isNight} is false:
                stop
            open chest with 5 rows named "마피아" to player
        if {mafia.%player%.jobCode} is 2:
            send "당신은 &9경찰&f입니다."
            if {mafia.isNight} is false:
                stop
            open chest with 5 rows named "경찰" to player
        if {mafia.%player%.jobCode} is 3:
            send "당신은 &f의사&f입니다."
            if {mafia.isNight} is false:
                stop
            open chest with 5 rows named "의사" to player
        loop 6 times:
            set {_n} to "%{mafia.players::%loop-value%}%"
            if {mafia.%{_n}%.jobCode} is not 4:
                set slot {mafia.click_format::%loop-value%} of current inventory of player to skull of "%{_n}%" parsed as player named "&f%{_n}%"

on inventory click:
    if iname contains "마피아":
        cancel event

        set {_i} to 1
        loop {mafia.click_format::*}:
            if clicked raw slot is loop-value:
                set {_n} to "%{mafia.players::%{_i}%}%"

                if {mafia.%{_n}%.jobCode} is not set:
                    stop
                if {mafia.%{_n}%.jobCode} is 4:
                    stop
                    
                set {mafia.%{_n}%.click_player} to {_i}
                send "-----------------------------------------------------"
                send "[%{_n}%]님을 지목하셨습니다."
                send "-----------------------------------------------------"
            add 1 to {_i}
        stop

    if iname contains "경찰":
        cancel event

        set {_i} to 1
        loop {mafia.click_format::*}:
            if clicked raw slot is loop-value:
                if {mafia.isClick} is not set:
                    set {_n} to "%{mafia.players::%{_i}%}%"

                    if {mafia.%{_n}%.jobCode} is not set:
                        stop
                    if {mafia.%{_n}%.jobCode} is 4:
                        stop
                    if {mafia.%{_n}%.jobCode} is 2:
                        send "본인을 선택하셨습니다. 다시 선택해주세요."
                        stop
                    
                    send "-----------------------------------------------------"
                    send "[%{_n}%]님을 지목하셨습니다."
                    if {mafia.jobs::%{_i}%} is 0:
                        send "[%{_n}%]님은 [&e시민&f] 입니다"
                    else if {mafia.jobs::%{_i}%} is 1:
                        send "[%{_n}%]님은 [&c마피아&f] 입니다"
                    else if {mafia.jobs::%{_i}%} is 3:
                        send "[%{_n}%]님은 [&f의사&f] 입니다"
                    send "-----------------------------------------------------"

                    set {mafia.isClick} to true

                    stop
                else:
                    send "이미 선택하셨습니다."
                    stop
            add 1 to {_i}
        stop

    if iname contains "의사":
        cancel event

        set {_i} to 1
        loop {mafia.click_format::*}:
            if clicked raw slot is loop-value:
                set {_n} to "%{mafia.players::%{_i}%}%"

                if {mafia.%{_n}%.jobCode} is not set:
                    stop
                if {mafia.%{_n}%.jobCode} is 4:
                    stop
                set {mafia.%{_n}%.click_player} to {_i}
                send "-----------------------------------------------------"
                send "[%{_n}%]님을 지목하셨습니다."
                send "-----------------------------------------------------"
            add 1 to {_i}
        stop
    
    if iname contains "투표":
        cancel event

        set {_i} to 1
        loop {mafia.click_format::*}:
            if clicked raw slot is loop-value:
                set {_n} to "%{mafia.players::%{_i}%}%"

                if {mafia.%{_n}%.jobCode} is not set:
                    stop
                set {mafia.%{_n}%.click_player} to {_i}
                send "-----------------------------------------------------"
                send "[%{_n}%]님을 지목하셨습니다."
                send "-----------------------------------------------------"
            add 1 to {_i}
        stop

every 1 seconds:
    # 보스바 타이머
    if {mafia.isStart} is true:
        remove 1 from {mafia.numerator}
        set skellett progress of bossbar {mafia.bar} to {mafia.numerator} / {mafia.denominator} 

        if {mafia.isNight} is true:
            set skellett title of bossbar {mafia.bar} to "남은 밤 시간 : %{mafia.numerator}%초"
        if {mafia.isVote} is true:
            set skellett title of bossbar {mafia.bar} to "남은 투표 시간 : %{mafia.numerator}%초"

        loop all players:
            if {mafia.%loop-player%.isChat} is true:
                skellett add loop-player to bossbar {mafia.bar}
                if {mafia.numerator} <= 0:
                    remove loop-player from bossbar {mafia.bar}

on chat:
    if {mafia.%player%.isChat} is true:
        cancel event

        if {mafia.%player%.jobCode} is 4:
            loop all players:
                if {mafia.%loop-player%.isChat} is true:
                    if {mafia.%loop-player%.jobCode} is 4:
                        set {_p} to "%loop-player%" parsed as player
                        send "&f[&9마피아 채널&f](&7유령&f)%player%: %message%" to {_p}
        else:
            if {mafia.isNight} is true: 
                if {mafia.%player%.jobCode} is 1: # 마피아
                    loop {mafia.players::*}:
                        if {mafia.%loop-value%.jobCode} is 1:
                            set {_p} to "%loop-value%" parsed as player
                            send "&f[&9마피아 채널&f](&c마피아&f)%player%: %message%" to {_p}
                loop all players:
                    if {mafia.%loop-player%.isChat} is true:
                        if {mafia.%loop-player%.jobCode} is 4:
                            set {_p} to "%loop-player%" parsed as player
                            send "&f[&9마피아 채널&f](&c마피아&f)%player%: %message%" to {_p}
            else:
                loop all players:
                    if {mafia.%loop-player%.isChat} is true:
                        set {_p} to "%loop-player%" parsed as player
                        send "&f[&9마피아 채널&f](&e???&f)%player%: %message%" to {_p}

on script load:
    execute console command "/mfinit"
    if {mafia.bar} is not set:
        set {mafia.bar} to skellett new bossbar

on quit:
    if {mafia.%player%.isAttend} is true:
        set {mafia.%player%.isAttend} to false
        remove 1 from {mafia.count}
        remove "%player%" from {mafia.players::*}